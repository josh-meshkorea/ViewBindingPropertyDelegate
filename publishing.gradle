apply plugin: 'maven-publish'

tasks.register("sourceJar", Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

Properties localProps = new Properties()
File localProperties = new File(rootProject.rootDir, "local.properties")
if (localProperties.exists() && localProperties.isFile()) {
    localProperties.withInputStream { localProps.load(it) }
}

afterEvaluate {
    publishing {
        publications {
            releaseAar(MavenPublication) {
                from components.release
                artifact sourceJar

                groupId = project.ext.groupId
                artifactId = project.ext.artifactId
                version = rootProject.ext.versionName

                pom {
                    name = 'View Binding Property Delegate'
                    description = 'Make work with Android View Binding simpler'
                    url = 'https://github.com/kirich1409/ViewBindingPropertyDelegate'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }

                    developers {
                        developer {
                            id = 'kirich1409'
                            name = 'Kirill Rozov'
                            email = 'krl.rozov@gmail.com'
                        }
                    }

                    scm {
                        url = 'https://github.com/kirich1409/ViewBindingPropertyDelegate'
                    }
                }
            }
        }

        if (meshRepoDeployUser != null && meshRepoDeployPassword != null) {
            repositories {
                maven {
                    url project.getMavenUrl()
                    credentials {
                        username = meshRepoDeployUser
                        password = meshRepoDeployPassword
                    }
                }
            }
        } else {
            repositories {
                maven {
                    name = "sonatypeStaging"
                    url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
                    credentials {
                        username = localProps.getProperty("ossrhUsername")
                        password = localProps.getProperty("ossrhPassword")
                    }
                }

                maven {
                    name = "sonatypeSnapshots"
                    url = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
                    credentials {
                        username = localProps.getProperty("ossrhUsername")
                        password = localProps.getProperty("ossrhPassword")
                    }
                }
            }
        }
    }
}
